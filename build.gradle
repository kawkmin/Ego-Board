plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.self'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    asciidoctorExt //Gradle에게 asciidoctorExt 필요하다고 알림
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    //Spring Rest Docs에서 필요로하는 Asciidoctor 확장을 추가하는 데 사용
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
}

tasks.named('test') {
    useJUnitPlatform()
}

/******* Start Spring Rest Docs *******/

ext {
    snippetsDir = file("$buildDir/generated-snippets") //테스트에서 생성된 코드 스니펫이 저장될 디렉토리를 지정
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir //JUnit 테스트를 실행하고, 생성된 코드 스니펫을 snippetsDir 디렉토리에 출력
}

asciidoctor { //의존성 주입된 asciidoctorExt를 설정으로, Asciidoctor를 사용하여 문서를 생성
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

asciidoctor.doFirst { //실행 전 docs 삭제
    delete file("src/main/resources/static/docs")
}

bootJar { //asciidoctor 태스크가 생성한 문서 파일을 JAR 파일에 포함
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into "static/docs"
    }
}

task copyDocument(type: Copy) {
    //asciidoctor 태스크에서 생성된 Asciidoctor 소스 파일을 src/main/resources/static/docs 디렉토리로 복사
    dependsOn asciidoctor
    from file("$buildDir/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build { //빌드가 실행될 때 copyDocument 태스크가 자동으로 실행 설정
    dependsOn copyDocument
}

/******* End Spring Rest Docs *******/
